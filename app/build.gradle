plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'com.kancanakid.ataskassignment.app'
    compileSdk 33

    defaultConfig {
        applicationId "com.kancanakid.ataskassignment.app"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "mode", "type"
    productFlavors{
        red {
            dimension "mode"
            applicationIdSuffix ".red"
            versionNameSuffix "-red"
        }
        green {
            dimension "mode"
            applicationIdSuffix ".green"
            versionNameSuffix "-green"
        }
        filesystem{
            dimension "type"
        }
        builtincamera{
            dimension "type"
        }

    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavour = variant.flavorName
            def newFlavor = ""
            switch(flavour){
                case "redFilesystem" : {
                    newFlavor = "red-filesystem"
                    break
                }
                case "redBuiltincamera" : {
                    newFlavor = "red-built-in-camera"
                    break
                }
                case "greenFilesystem" : {
                    newFlavor = "green-filesystem"
                    break
                }
                case "greenBuiltincamera" : {
                    newFlavor = "green-built-in-camera"
                    break
                }
            }

            def newFileName = "app-" + newFlavor +".apk"
            output.outputFileName = newFileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig  true
    }
    hilt {
        enableAggregatingTask = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lintOptions {
        disable 'ContentDescription'
        abortOnError false
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"
    implementation "androidx.compose.runtime:runtime-livedata:1.4.3"
    implementation 'androidx.compose.material3:material3'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.31.0-alpha"
    implementation "com.google.accompanist:accompanist-permissions:0.27.0"

    // ML Kit
    implementation 'com.google.mlkit:text-recognition:16.0.0'

    // Coil
    implementation("io.coil-kt:coil-compose:2.3.0")

    //dagger-hilt
    implementation 'com.google.dagger:hilt-android:2.45'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    kapt 'com.google.dagger:hilt-android-compiler:2.45'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    //room
    implementation "androidx.room:room-runtime:2.5.1"
    implementation "androidx.room:room-ktx:2.5.1"
    kapt "androidx.room:room-compiler:2.5.1"
    implementation("androidx.room:room-rxjava3:2.5.1")

    //gson
    implementation 'com.google.code.gson:gson:2.9.0'

    //splash
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // CameraX
    def camerax_version = "1.0.1"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:1.3.0-alpha07"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

kapt {
    correctErrorTypes = true
}